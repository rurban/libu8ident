#!/usr/bin/env perl
# libu8ident - Check unicode security guidelines for identifiers.
# Copyright 2014, 2021 Reini Urban
# SPDX-License-Identifier: Apache-2.0
#
# Create mark.h Combining_Mark (Mc | Me | Mn)

use strict;
use Config;
my $pva = "PropertyValueAliases.txt";
my $wb = "auxiliary/WordBreakProperty.txt"; #see Extend
my $ucd = "UnicodeData.txt";
for ($pva, $ucd) {
  if (!-e $_) {
    system("wget -N https://www.unicode.org/Public/UNIDATA/$_");
  }
}

my @MARK;
open my $UCD, "<", $ucd or die "$ucd $!";
while (<$UCD>) {
  my @l = split ';';
  my $mark = @l[2];
  push @MARK, hex($l[0]) if $mark =~ /^M[cen]$/;
}
close $UCD;

open my $H, ">", "mark.h" or die "writing mark.h $!";
print $H <<"EOF";
/* ex: set ro ft=c: -*- mode: c; buffer-read-only: t -*- */
/* libu8ident - Check unicode security guidelines for identifiers.
   Copyright 2014, 2021 Reini Urban
   SPDX-License-Identifier: Apache-2.0

   generated by mkmark.pl, do not modify.
*/

/* All Combining Marks, sorted */
#ifndef EXT_SCRIPTS
const uint32_t mark_list[] = {
    // clang-format off
EOF
for my $m (@MARK) {
  printf $H "    0x%X,\n", $m;
}
printf $H <<'EOF', scalar @MARK;
    // clang-format on
};
#else
extern const uint32_t mark_list[%u];
#endif
EOF

if (-e "roaring.c") {
    print "Create serialized roaring bitmaps:\n";
    if ($^O =~ /Win32/) {
	system($Config{cc}." mkroar.c -I. -o mkroar.exe");
	system("mkroar.exe mark");
	# ignore vms for now
    } else {
	system($Config{cc}." mkroar.c -I. -o mkroar");
	system("./mkroar mark");
    }
    print "\n";
    my $c = "mark_croar";
    print $H "\n#ifdef HAVE_CROARING\n";
    print $H "/* generated via mkroar.c */\n";
    close $H;
    system("xxd -i $c.bin >> mark.h");
    unlink "$c.bin";
    open $H, ">>", "mark.h" or die "appending mark.h $!";
    print $H "#endif\n";
}

close $H;
print "Created mark.h\n";

